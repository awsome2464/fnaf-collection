label getting_a_job:
    $currentStory = "FNaF"
    macey "Come on, Mike!"
    scene bg apartment_tv
    show macey straight mad blank at right
    with dissolve
    macey "You can't sit on this couch forever!"
    mike "Yes, I can!"
    "I replied without taking my eyes off the TV."
    show macey:
        ease 0.75 middle
    "She stood in front of the TV to try and block it."
    macey "You really think you're going to make money by watching TV all day?"
    "I adjusted my head to look around her before replying:"
    mike "I'm sure it's possible somewhere."
    macey "Well, it's not possible here!"
    show macey:
        ease 0.75 middle_close
    "She then shut off the TV and bent down to look me eye-to-eye."
    play music movement
    macey sad "Look, when you dropped out of high school, I was the only one who was more or less on your side. You remember that, right?"
    mike "Of course."
    "Where exactly is she going with this?"
    macey "To this day, Mom still tells me that letting you live with me is being too generous, that I'm doing nothing but coddle you and letting you take my hospitality for granted."
    macey level "And while I disagree with how she worded it, she's not exactly wrong, Mike."
    mike "What, are you going to kick me out?"
    macey "No.{w=0.5}{nw}"
    extend mad " But I {i}am{/i} going to kick you in the pants harder than I have been. {w=0.2}Which was basically not at all."
    mike "Can't it wait some other time?"
    "I reached for the remote, {nw}"
    show macey:
        ease 0.1 xalign 0.65
    extend "but she grabbed my wrist tightly."
    mike "Ow!{w=0.2} What are you--"
    macey level "Mike."
    "She had an uncharacteristically serious expression on her face."
    macey "I'm being serious here.{w=0.2} I'd appreciate it if you were, too."
    "I sighed and nodded, signalling for her to continue."
    show macey:
        linear 1.0 middle_close
    "She let go of my wrist and regained her composure."
    macey sad "I promised Mom that I would help you have a successful life. {w=0.2}And you're never going to have that if you don't start with at least getting a job somewhere."
    mike "Mace, you know me.{w=0.2} I'm not really a hard-working guy."
    macey "I know.{w=0.2} But Mom's getting really impatient, Mike."
    macey raised "Do you really want to keep looking her in the eye and telling her that your life situation has in no way improved?"
    "I stared at her in thought for a few seconds."
    "As much as I hated working, I hated disappointing my mom even more."
    mike "Okay, fine.{w=0.2} I'll at least look at what's being offered."
    macey normal smile "That's the spirit!"
    hide macey with easeoutright
    play sound door_close
    "She then went out to do some shopping for that evening's meal."
    window hide dissolve
    nvl show dissolve
    narrate """
    Looking back, I really don't know what I was thinking when I decided to drop out of high school.

    I guess maybe I thought I was being rebellious against the high standards my family had for me?

    Regardless of the reason, that was around 3 years ago now, and things aren't exactly going well for me, as Macey said.

    I've got no job or money, I'm crashing at my sister's apartment, and my parents are heavily disappointed in me.

    I suppose it hasn't been the worst of experiences so far, but deep down, I know I can do better.

    Or maybe I just want to prove Mom and Dad wrong.{w=0.2} That I actually CAN amount to something in life.

    Either way, the time for mooching is over.{w=0.2} I gotta try and find a job somewhere.
    """
    nvl hide
    with dissolve
    scene bg apartment_dining with dissolve
    nvl clear
    nvl show dissolve
    narrate """
    I went over to today's paper (which Macey conveniently left on the dining room table) and began looking through the Want Ads.

    Cashier?{w=0.5} If I wanted to deal with random idiots for hours on end, I'd go on the internet.

    Truck driver?{w=0.5} There's no way in Hell I'm getting a CDL for that.

    Fry cook?{w=0.5} I can barely cook a meal for {i}myself{/i} let alone the general public.

    {nw}

    Every other job listing I could find required some sort of degree or other prerequisite.

    Eventually, I just tossed the paper back on the table with a sigh.

    By that time, Macey had returned home.
    """
    nvl hide
    with dissolve
    pause 0.5
    show macey normal blank straight at middle_close with easeinleft
    window show dissolve
    macey "Find anything?"
    mike "Not really."
    macey level "Mike."
    mike "I'm serious, Mace.{w=0.2} There's nothing here that I really feel like I'd enjoy."
    macey "I'd argue that you're not exactly in a position to be picky, Mike."
    "She then picked up a red marker and the newspaper and glanced through it, herself."
    macey raised "Hm.{w=0.2} I see a lot of these require at least a high school diploma..."
    mike "Exactly."
    "She continued glancing through the articles, using the marker as a pointer as she did."
    macey normal "Oh!{w=0.2} Here's one that doesn't require one."
    "She took the cap off the marker and circled one of the articles before handing the paper back to me."
    window hide dissolve
    pause 0.5
    scene cg help_wanted with longdissolve
    pause 1
    window show dissolve
    mike "Freddy Fazbear's Pizza..."
    mike "Isn't that the old pizza place near the edge of town?"
    macey "I think so.{w=0.2} I didn't think it was still open, to be honest."
    mike "Still, don't you think it's weird that they don't want any sort of qualifications to be a security guard?"
    macey "Maybe a little, but if this ad is anything to go by, then it doesn't seem like difficult work."
    macey "After all, sitting on your ass and staring at a screen for hours on end is your best skill~"
    mike "Ah, fuck you."
    window hide dissolve
    pause 0.5
    scene bg apartment_dining
    show macey straight normal smile at middle_close
    with dissolve
    pause 0.5
    window show dissolve
    macey "I really think you should apply for this one, in all seriousness."
    macey level left blank "If only for the simple fact that it's the only one you're currently qualified for..."
    mike "I dunno, Macey..."
    macey straight sad "Mike, at least give them a call.{w=0.2} What's the harm?"
    mike "..."
    mike "Alright, fine.{w=0.2} I'll give them a call."
    macey smile "Thank you."
    macey "And if nothing else, don't feel like you're doing it for me or money.{w=0.2} You're doing it for Mom and Dad."
    mike "If you say so."
    hide macey with dissolve
    "She then started prepping dinner."
    "While she did, I silently groaned before calling the number."
    "I didn't think that a security guard was something I'd ever have as a job, period, let alone my first one, but here we are."
    "Though, to be honest, maybe Macey was right.{w=0.5}\nMaybe it won't be such a hard job."
    window hide dissolve
    stop music fadeout(5)
    pause 2
    scene bg black with longdissolve
    pause 3
    $persistent.story1Chapters["ch1"] = True
    jump interview
